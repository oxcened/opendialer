plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs.kotlin'
    id 'com.google.dagger.hilt.android'
    alias(libs.plugins.compose.compiler)
    alias(libs.plugins.kotlin.serialization)
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def shouldSign = keystorePropertiesFile.exists()
def keystoreProperties = new Properties()

if (shouldSign) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    signingConfigs {
        if (shouldSign) {
            release {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    }
    defaultConfig {
        applicationId "dev.alenajam.opendialer"
        compileSdk 34
        minSdkVersion 24
        targetSdkVersion 33
        versionCode 1
        versionName "0.1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            if (shouldSign) {
                signingConfig signingConfigs.release
            }
        }
        debug {
            debuggable true
            versionNameSuffix '-debug'
        }
    }
    buildFeatures {
        viewBinding true
        compose true
    }
    namespace 'dev.alenajam.opendialer'
    lint {
        disable 'MissingTranslation'
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
    }
    composeOptions {
        kotlinCompilerExtensionVersion = "1.5.2"
    }
}

dependencies {
    implementation(project(":feature:calls"))
    implementation(project(":feature:callDetail"))
    implementation(project(":feature:contacts"))
    implementation(project(":feature:inCall"))
    implementation(project(":feature:contactsSearch"))
    implementation(project(":feature:settings"))
    implementation(project(":data:calls"))
    implementation(project(":core:common"))

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.annotation:annotation:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation "androidx.core:core-ktx:1.12.0"
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation "androidx.fragment:fragment-ktx:1.6.2"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.2"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.6.2"
    implementation "androidx.lifecycle:lifecycle-reactivestreams-ktx:2.6.2"
    implementation 'androidx.navigation:navigation-fragment-ktx:2.7.5'
    implementation 'androidx.navigation:navigation-ui-ktx:2.7.5'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4'

    implementation 'com.google.android.material:material:1.10.0'
    implementation 'com.google.code.gson:gson:2.9.0'

    implementation 'com.google.dagger:hilt-android:2.48.1'
    kapt 'com.google.dagger:hilt-compiler:2.48.1'
    androidTestImplementation 'com.google.dagger:hilt-android-testing:2.48.1'
    kaptAndroidTest 'com.google.dagger:hilt-compiler:2.48.1'
    testImplementation 'com.google.dagger:hilt-android-testing:2.48.1'
    kaptTest 'com.google.dagger:hilt-compiler:2.48.1'

    androidTestImplementation 'androidx.test.ext:junit:1.2.1'

    testImplementation 'junit:junit:4.13.2'
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.4'

    def composeBom = platform('androidx.compose:compose-bom:2024.09.03')
    implementation composeBom
    androidTestImplementation composeBom
    implementation(libs.androidx.material3)
    implementation 'androidx.compose.ui:ui-tooling-preview'
    debugImplementation 'androidx.compose.ui:ui-tooling'
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'
    implementation 'androidx.compose.material:material-icons-extended'
    implementation "androidx.compose.ui:ui-viewbinding"
    implementation(libs.navigation.compose)
    implementation(libs.kotlinx.serialization)
}

repositories {
    mavenCentral()
}

kotlin {
    jvmToolchain 17
}

kapt {
    correctErrorTypes true
}

hilt {
    enableAggregatingTask = true
}